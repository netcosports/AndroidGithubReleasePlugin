/*
 * Copyright (c) 2019. Trevor Jones
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: 'kotlin'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: "com.gradle.plugin-publish"

gradlePlugin {
  plugins {
    AGRP {
      displayName = "com.trevjonez.AndroidGithubReleasePlugin"
      description = "Android variant aware tasks for creating github releases and uploading assets"
      id = "com.trevjonez.AndroidGithubReleasePlugin"
      implementationClass = "com.trevjonez.agrp.AndroidGithubReleasePlugin"
    }
  }
}

pluginBundle {
  website = "https://github.com/trevjonez/AndroidGithubReleasePlugin"
  vcsUrl = "https://github.com/trevjonez/AndroidGithubReleasePlugin.git"
  tags = ["android", "github", "release", "tag"]
}

tasks.named("test").configure {
  systemProperty("org.gradle.testkit.debug", true)
  systemProperty("agpVersion", agpVersion)
  systemProperty("pluginTestingToken", findProperty("pluginTestingToken"))
}

tasks.withType(KotlinCompile).all {
  kotlinOptions {
    jvmTarget = "1.8"
    freeCompilerArgs += "-Xjsr305=strict"
    freeCompilerArgs += '-XXLanguage:+NewInference'
  }
}

dependencies {
  api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  api "com.trevjonez.github:releases:0.2.0"
  api "com.trevjonez.github:gradle-helpers:0.2.0"
  api "com.android.tools.build:gradle:$agpVersion"

  testImplementation 'junit:junit:4.12'
  testImplementation 'org.assertj:assertj-core:3.5.2'
}

task sourcesJar(type: Jar, dependsOn: classes) {
  getArchiveClassifier().set('sources')
  from sourceSets.main.allSource
}
